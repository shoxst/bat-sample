# 入出力・変数

## 標準入力
```bat
set /p input=文字を入力:
echo %input%
```

## 改行のみ出力
```bat
echo.
echo,
```

## 改行せずに出力
```bat
set /p dummy=AAA<nul
set /p dummy=BBB<nul
```
```
AAABBB
```

## ファイルへのリダイレクト
```bat
@rem 標準出力のリダイレクト/新規作成
command > file

@rem 標準出力のリダイレクト/追記
command >> file

@rem 標準エラー出力のリダイレクト/新規作成
command 2> file

@rem 標準エラー出力のリダイレクト/追記
command 2>> file

@rem 標準出力&標準エラー出力のリダイレクト
command > file1 2> file2

@rem 標準出力&標準エラー出力の同一ファイルへのリダイレクト
command >> file 2>&1
```

## 変数の設定と参照
```bat
set var1=abc
set var2="abc"
set var3=10
set /a var4 = var3 + 1

echo %var1%
echo %var2%
echo %var3%
echo %var4%
```
```
abc
"abc"
10
11
```

変数をセットする際には、=の前後にスペースを含めないようにする。スペースも変数名や値として認識されてしまうため。計算結果を変数に代入するには、`set /a`を使う。この場合は、スペースあってもOK。変数の参照は、変数名を`%`でくくる。

## 遅延変数
```bat
set var1=abc
set cnt=0
if %var1% == abc (
    set /a cnt += 1
    echo %cnt%
)
```
```
0
```
if文が最後の括弧閉じまでで1つの命令のため、命令実行前に`%cnt%`が0で置き換えられる。`cnt += 1`の計算自体は行われているので、if文実行後、`cnt`には1が格納されている。実行時に変数解釈を行うためには遅延変数を用いる。

```bat
setlocal enabledelayedexpansion
set var1=abc
set cnt=0
if %var1% == abc (
    set /a cnt += 1
    echo !cnt!
)
```
```
1
```
遅延変数の参照は!でくくる。

## 日付・時刻
```bat
echo %date%
echo %time%
```
```
2021/06/27
13:31:41.07
```
if文やfor文の中でタイムスタンプつけたいときは遅延変数として参照するように注意。

また、time環境変数について0時から9時の場合は時刻部分のみ、0ではなく半角スペースで桁合わせする仕様なのでタイムスタンプをファイル名にするときなどは注意。

## 文字列操作
### 結合
```bat
set var1=AAA
set var2=BBB
set var3=%var1%%var2%

echo %var3%
echo var3は%var1%%var2%です
```
```
AAABBB
var3はAAABBBです
```

### 抽出
```
%<変数名>:~<開始位置>,<文字数>%
```
開始位置について先頭文字は0文字カウント、負の値は末尾からのカウント
```bat
set var1=abcde12345
echo %var1:~3%
echo %var1:~3,5%
echo %var1:~-7,-5%
```
```
de12345
de123
de
```

### 置換
```
%<変数名>:<置換前文字列>:<置換後文字列>%
```
```bat
@REM set thisDate=%date%
@REM set thisTime=%time%
set thisDate=2021/06/27
set thisTime= 9:01:01.02

set custDate=%thisDate:/=%

set custTime=%thisTime: =0%
set custTime=%custTime:~0,8%
set custTime=%custTime::=%

echo original  : %thisDate% %thisTime%
echo customized: %custDate% %custTime%
```
```
original  : 2021/06/27  9:01:01.02
customized: 20210627 090101
```
timestampの取得方法については、`02_FlowControl\sample-get-timestamp`の処理も参考のこと。

## 環境変数の存在確認
`defined <変数>`で変数が定義されているか、すなわちその環境変数が存在するかを確認する。
```bat
set a=1
if defined a (
    echo true
) else (
    echo false
)
if defined b (
    echo true
) else (
    echo false
)
```
```
true
false
```

## 多重変数展開
`call`を1回評価するたびにその変数が展開されるので、多重展開が可能となる。

```bat
set start=3
set length=5
set var1=abcde12345
echo %%var1:~%start%,%length%%%
call echo %%var1:~%start%,%length%%%
```
```
%var1:~3,5%
de123
```

## 配列
配列という概念はないので、インデックスを含めた別変数として定義する。
```bat
set arr[0]=aaa
set arr[1]=bbb
set arr[2]=ccc

for /l %%i in (0,1,2) do (
    echo %arr[%%i]%
    echo %%arr[%%i]%%
    call echo %%arr[%%i]%%
    echo,
)
```
```
ECHO は <OFF> です。
%arr[0]%
aaa

ECHO は <OFF> です。
%arr[1]%
bbb

ECHO は <OFF> です。
%arr[2]%
ccc
```
遅延変数を使うなら
```bat
setlocal enabledelayedexpansion

set arr[0]=aaa
set arr[1]=bbb
set arr[2]=ccc

for /l %%i in (0,1,2) do (
    echo !arr[%%i]!
)
```
```
aaa
bbb
ccc
```

## コマンド実行結果の変数への代入
```
for /f <オプション> %%<変数> in (`コマンド`) do 
```
```bat
for /f "usebackq" %%a in (`hostname`) do (
    set hostName=%%a
)
echo %hostName%
```
コマンド結果が複数行あるときもファイル読み込みのときと同じ。複数列あるときは`tokens`オプションで分割して取得可能。


## リテラル文字列内に()を含める
```bat
if not exist %filePath% (
    echo ファイルが見つかりません ^(%filePath%^)
)
```
`^`でエスケープするか全角の`（）`を使わないと`if`の閉じ括弧と解釈されて文字列内の`)`が消えてしまう。
