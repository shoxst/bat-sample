# ファイル操作

## パス名の置換
`%0`は実行されているバッチファイルのパス。
|  参照方法 |  実行結果  |
| -------- | ---- |
|  `%~f0` = `%~dpnx0` |  C:\tmp\bat-sample\test.bat  |
|  `%~d0`  |  C:  |
|  `%~p0`  |  \tmp\bat-sample\  |
|  `%~n0`  |  test  |
|  `%~x0`  |  .bat  |

## 親ディレクトリのパス取得
```bat
pushd %~dp0..
set dir1=%cd%
popd
```

## ディレクトリの存在確認
パスの末尾に`\*`をつける。ローカルパス、UNCパス、ネットワークドライブパスいずれもOK。
```bat
if exist %dir1%\* (
    echo true
) else (
    echo false
)
```

## ファイルの存在確認
同名のディレクトリが存在しない、かつファイルまたはディレクトリが存在する、で確認する。
```bat
if not exist %file1%\* (
    if exist %file1% (
        echo true
    )
)
```

## ファイル・フォルダ一覧を取得
```
for %%<変数> in (<ファイルセット>) do
```
`/r`オプションを指定すると、特定のディレクトリを指定可能で、サブディレクトリ以下も操作対象になる。`/d`オプションでフォルダ名の一覧を取得する。
```bat
for %%i in (*.txt) do (
    echo %%i
)
echo,

for /r %%i in (*.txt) do (
    echo %%i
)
echo,

for /r .\folder1 %%i in (*.txt) do (
    echo %%i
)
```
```
file1.txt

C:\tmp\bat-sample\03_FileManipulation\file1.txt
C:\tmp\bat-sample\03_FileManipulation\folder1\file2.txt

C:\tmp\bat-sample\03_FileManipulation\folder1\file2.txt
```

## ファイルの読込
```
for /f <オプション> %%<変数> in (<ファイル名>) do
```
```
Name Code Phone
AAA 001 11111
BBB 002 22222
CCC 003 33333
```
```bat
for /f "skip=1 tokens=1-3 usebackq" %%a in ("file1.txt") do (
    echo name %%a
    echo code %%b
    echo phone %%c
    echo.
)
```
```
name AAA
code 001
phone 11111

name BBB
code 002
phone 22222

name CCC
code 003
phone 33333
```
オプションは以下の通り。
- `skip`
  - スキップする行数を指定。
- `delims`
  - 区切り文字、省略するとスペースかタブになる。
- `tokens`
  - 取得する列をカンマ区切りで指定する。ハイフンで範囲指定も可能。末尾にアスタリスクを指定すると、それ以降を1つの列として取得する。
- `usebackq`
  - ファイル名をダブルクォーテーションでくくることができる。
