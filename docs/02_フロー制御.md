# フロー制御

## 条件分岐
### 文字列・数値比較
```bat
set str1=abc

if %str1% == abc (
    echo true
) else (
    echo false
)

if %str1% equ abc (
    echo true
) else (
    echo false
)

set num1=255

if %num1% == 0xff (
    echo true
) else (
    echo false
)

if %num1% equ 0xff (
    echo true
) else (
    echo false
)
```
```
true
true
false
true
```
`==` で比較すると必ず文字列として比較する。比較演算子 `equ(==), neq(!=), lss(<), leq(<=), gtr(>), geq(>=)`を使う場合、数値のみなら数値として比較、数値以外が含まれれば文字列として比較する。

また例えば引数が空かどうかを確認するような場合はダブルクオーテーションでくくる。
```bat
if "%1" == "" (
  echo 引数を指定してくだい
)
```
`%1`が空文字だった場合、
```bat
if %1 == "" (
  echo 引数を指定してくだい
)
```
```bat
if == "" (
  echo 引数を指定してくだい
)
```
と展開されて構文エラーになる。

### エラー判定
errorlevelが番号以上なら
```
if errorlevel <番号>
```
errorlevelが番号以上でないなら = 番号未満なら
```
if not errorlevel <番号>
```

## for
```
for /l %%<変数> in (<開始値>,<増分>,<終了値>) do
```
```bat
setlocal enabledelayedexpansion

set sum=0
for /l %%i in (1,1,10) do (
    set /a sum += %%i
    echo !sum! 
)
```
```
1
3
6
...
55
```
`in`と`(`の間にスペースを入れないとエラー。

## foreach
callによる多重展開か遅延変数で実装可能。
```bat
set arr[0]=aaa
set arr[1]=bbb
set arr[2]=ccc

set i=0
:loop
call set elm=%%arr[%i%]%%
if defined elm (
    echo %elm%
    set /a i+=1
    goto loop
)
```

## goto
ラベルに対して`goto`を使用すると、ラベル位置に制御が移動する。
```bat
echo start
goto label_a
echo end

:label_a
echo label a

:label_b
echo label b
```
```
start
label a
label b
```

## サブルーチン
### call
`call`を使用すると、ラベル以降をサブルーチンとして呼び出す。callでは引数を渡すことも可能。
```bat
echo start
call :label_a arg1
echo end

:label_a
echo label a
echo %1

:label_b
echo label b
```
```
start
label a
arg1
label b
end
```
途中で制御を戻すには、`exit /b`。`errorlevel`を返すなら、`exit /b <番号>`
```bat
echo start
call :label_a arg1
echo %errorlevel%
echo end

:label_a
echo label a
echo %1
exit /b 99

:label_b
echo label b
```
```
start
label a
arg1
99
end
```

### ローカル変数
`setlocal`で変数のスコープがサブルーチン内に限定される。サブルーチンから戻る際に自動で`endlocal`されるので`endlocal`はあってもなくてもよい。
```bat
set var1=aaa

echo [1] %var1%
call :label_a
echo [4] %var1%

:label_a
setlocal
echo [2] %var1%
set var1=bbb
echo [3] %var1%
exit /b
```
```
[1] aaa
[2] aaa
[3] bbb
[4] aaa
```

次のように書けば、ほぼ関数のように使える。
```bat
@echo off
cd /d %~dp0

call :getTimeStamp
echo %timeStamp%
pause > nul

:getTimeStamp
    setlocal

    call set yyyymmdd=%%date:/=%%
    call set hhmmss=%%time:~0,8%%
    set hhmmss=%hhmmss::=%
    set hhmmss=%hhmmss: =0%

    endlocal & set timeStamp=%yyyymmdd%-%hhmmss%
exit /b
```

## バッチ呼び出し
### ex1. 直接指定
バッチファイルを直接指定すると、呼び出し先バッチで`exit /b`しても制御が戻ってこないので、あまり使えない。
```bat
@REM sample-ex1.bat
@echo off
cd /d %~dp0

set var1=aaa
sample-sub.bat arg1
echo [main] %errorlevel%
echo [main] %var1%

pause > nul
```
```bat
@REM sample-sub.bat
@echo off
cd /d %~dp0

echo [sub] %var1%
set var1=bbb
echo [sub] %1

exit /b 2
```
```
[sub] aaa
[sub] arg1
```

### ex2. `call`
親と子は同じプロセスで実行され、変数を引き継いでいる。
```bat
@REM sample-ex2.bat
@echo off
cd /d %~dp0

set var1=aaa
call sample-sub.bat arg1
echo [main] %errorlevel%
echo [main] %var1%

pause > nul

```
```bat
@REM sample-sub.bat
@echo off
cd /d %~dp0

echo [sub] %var1%
set var1=bbb
echo [sub] %1

exit /b 2
```
```
[sub] aaa
[sub] arg1
[main] 2
[main] bbb
```

子の変数スコープを`setlocal`にしてしまえば、親の変数には引き継がれない。
```bat
@REM sample-sub-local.bat
@echo off
cd /d %~dp0
setlocal

echo [sub] %var1%
set var1=bbb
echo [sub] %1

exit /b 2
```
```
[sub] aaa
[sub] arg1
[main] 2
[main] aaa
```

### ex3. `cmd`
親と子が別プロセスで実行され、親で設定した変数は子に引き継がれるが、子で設定した変数は親に引き継がれない。
```bat
@REM sample-ex2.bat
@echo off
cd /d %~dp0

set var1=aaa
cmd /c sample-sub.bat arg1
echo [main] %errorlevel%
echo [main] %var1%

pause > nul

```
```bat
@REM sample-sub.bat
@echo off
cd /d %~dp0

echo [sub] %var1%
set var1=bbb
echo [sub] %1

exit /b 2
```
```
[sub] aaa
[sub] arg1
[main] 2
[main] aaa
```

### ex4. `start`
親と子は別プロセス非同期で実行される。親のバッチは子のバッチ終了を待たず、後続処理を行う。呼び出先は別ウィンドウが開かれる。`start /b`とすると、別ウィンドウが開かれないだけで、その他の挙動は同じ。子の最後で`/b`オプションつきで`exit`を実行すると、子プロセスが残り続けてしまう。
```bat
@REM sample-ex2.bat
@echo off
cd /d %~dp0

set var1=aaa
start /b sample-sub.bat arg1
echo [main] %errorlevel%
echo [main] %var1%

pause > nul

```
```bat
@REM sample-sub.bat
@echo off
cd /d %~dp0

echo [sub] %var1%
set var1=bbb
echo [sub] %1

exit /b 2
```
```
[main] 0
[main] aaa
[sub] aaa
[sub] arg1
```

### まとめ
- 同期呼び出し
  - 同一プロセス `call`
  - 別プロセス `cmd`
- 非同期呼び出し
  - `start /b`
